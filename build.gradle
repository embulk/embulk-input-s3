plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "java"
    id "maven-publish"
    id "jacoco"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.4.2" apply false
}

group = "org.embulk.input.s3"
version = "0.4.0"

allprojects {
    apply plugin: "java"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        options.encoding = "UTF-8"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
}

subprojects {
    apply plugin: "com.jfrog.bintray"

    bintray {
        // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
        user = project.hasProperty('bintray_user') ? bintray_user : ''
        key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

        publications = ['mavenJava']
        publish = true

        pkg {
            userOrg = 'embulk-input-s3'
            repo = 'maven'
            name = project.name
            desc = project.description
            websiteUrl = 'https://github.com/embulk/embulk-input-s3'
            issueTrackerUrl = 'https://github.com/embulk/embulk-input-s3/issues'
            vcsUrl = 'https://github.com/embulk/embulk-input-s3.git'
            licenses = ['Apache-2.0']
            labels = ['embulk', 'java']
            publicDownloadNumbers = true

            version {
                name = project.version
            }
        }
    }
}

subprojects {
    // embulk-input-s3, embulk-input-riak_cs
    apply plugin: "maven-publish"
    apply plugin: "org.embulk.embulk-plugins"

    test {
        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    jar {
        from rootProject.file("LICENSE")
    }

    sourcesJar {
        from rootProject.file("LICENSE")
    }

    javadocJar {
        from rootProject.file("LICENSE")
    }

    publishing {
        publications {
             mavenJava(MavenPublication) {
                afterEvaluate { project ->
                    groupId = "${project.group}"
                    artifactId = "${project.name}"
                }

                 from components.java
                 // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
                 // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

                pom {  // https://central.sonatype.org/pages/requirements.html
                    packaging "jar"

                    afterEvaluate { project ->
                        name = "${project.name}"
                        description = "${project.description}"
                    }
                    url = "https://www.embulk.org/"

                    licenses {
                        license {
                            // http://central.sonatype.org/pages/requirements.html#license-information
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            name = "Sadayuki Furuhashi"
                            email = "frsyuki@gmail.com"
                        }
                        developer {
                            name = "Muga Nishizawa"
                            email = "muga.nishizawa@gmail.com"
                        }
                        developer {
                            name = "Huy Le"
                            email = "huy.lenq@gmail.com"
                        }
                        developer {
                            name = "Satoshi Akama"
                            email = "satoshiakama@gmail.com"
                        }
                        developer {
                            name = "Trung Huynh"
                            email = "httrung90@gmail.com"
                        }
                        developer {
                            name = "Serhii Himadieiev"
                            email = "gimadeevsv@gmail.com"
                        }
                        developer {
                            name = "Dai MIKURUBE"
                            email = "dmikurube@treasure-data.com"
                        }
                    }

                    scm {
                        connection = "scm:git:git://github.com/embulk/embulk-input-s3.git"
                        developerConnection = "scm:git:git@github.com:embulk/embulk-input-s3.git"
                        url = "https://github.com/embulk/embulk-input-s3"
                    }
                }
            }
        }
    }

    gemPush {
        host = "https://rubygems.org"
    }

    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '6.14.1'
    }
    checkstyleMain {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    checkstyleTest {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    task checkstyle(type: Checkstyle) {
        classpath = sourceSets.main.output + sourceSets.test.output
        source = sourceSets.main.allJava + sourceSets.test.allJava
    }
}
